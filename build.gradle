// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {

    ext {
        BUILD_TOOLS_VERSION = '26.0.0-rc1'
        COMPILE_SDK_VERSION = 25
        MIN_SDK_VERSION = 14
        TARGET_SDK_VERSON = 25
        KOTLIN_VERSION = '1.1.2-3'
        ANDROID_GRADLE_VERSION = '2.3.0'
        ANDROID_SUPPORT_VERSION = '25.3.1'
        DAGGER_VERSION = '2.11-rc2'
        GSON_VERSION = '2.8.0'
        SLF4J_ANDROID_VERSION = '1.7.19'
        EVENT_BUS_VERSION = '3.0.0'
        DIRCHOOSER_VERSION = '3.2'
    }

    repositories {
        maven { url "http://maven.aliyun.com/nexus/content/groups/public"}
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$KOTLIN_VERSION"
        classpath "com.android.tools.build:gradle:$ANDROID_GRADLE_VERSION"
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {

    compileSdkVersion COMPILE_SDK_VERSION
    buildToolsVersion BUILD_TOOLS_VERSION

    dataBinding {
        enabled = true
    }

    defaultConfig {
        applicationId "org.mdvsc.vcserver"
        versionCode 1
        versionName '1.0.0'
        minSdkVersion MIN_SDK_VERSION
        targetSdkVersion TARGET_SDK_VERSON
    }

    signingConfigs {

        release {

            Properties properties = new Properties()
            try {
                properties.load(project.rootProject.file('local.properties').newDataInputStream())
            } catch (Exception ignored) {
                println('cannot find local.properties.')
            }
            String keystoreFileProperty = properties.getProperty('KEYSTORE')
            String keystorePasswordProperty = properties.getProperty('KEYSTORE_PASSWORD')
            String keyAliasProperty = properties.getProperty('KEY_ALIAS')
            String keyPasswordProperty = properties.getProperty('KEY_PASSWORD')

            keystoreFileProperty = keystoreFileProperty == null ? System.getenv('KEYSTORE') : keystoreFileProperty
            keystorePasswordProperty = keystorePasswordProperty == null ? System.getenv('KEYSTORE_PASSWORD') : keystorePasswordProperty
            keyAliasProperty = keyAliasProperty == null ? System.getenv('KEY_ALIAS') : keyAliasProperty
            keyPasswordProperty = keyPasswordProperty == null ? System.getenv('KEY_PASSWORD') : keyPasswordProperty

            if (keystoreFileProperty != null) {
                storeFile file(keystoreFileProperty)
                storePassword keystorePasswordProperty
                keyAlias keyAliasProperty
                keyPassword keyPasswordProperty
            }

        }

    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }

    lintOptions {
        abortOnError false
    }

    sourceSets {
        main {
            java.srcDirs += 'src/main/kotlin'
            jniLibs.srcDirs = ['libs']
        }
    }
}

repositories {
    maven { url "http://maven.aliyun.com/nexus/content/groups/public"}
    mavenCentral()
    jcenter()
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'org.apache.sshd:sshd-core:0.14.0'
    compile 'org.apache.mina:mina-core:2.0.13'
    compile 'org.eclipse.jgit:org.eclipse.jgit:3.7.1.201504261725-r'
    compile "net.rdrei.android.dirchooser:library:$DIRCHOOSER_VERSION@aar"
    compile "org.greenrobot:eventbus:$EVENT_BUS_VERSION"
    compile "org.slf4j:slf4j-android:$SLF4J_ANDROID_VERSION"
    compile "com.google.code.gson:gson:$GSON_VERSION"
    compile "org.jetbrains.kotlin:kotlin-stdlib:$KOTLIN_VERSION"
    compile "com.android.support:design:$ANDROID_SUPPORT_VERSION"
    compile "com.android.support:preference-v14:$ANDROID_SUPPORT_VERSION"
    compile "com.android.support:recyclerview-v7:$ANDROID_SUPPORT_VERSION"
    compile "com.google.dagger:dagger:$DAGGER_VERSION"
    kapt "com.google.dagger:dagger-compiler:$DAGGER_VERSION"
    kapt "com.android.databinding:compiler:$ANDROID_GRADLE_VERSION"
}
